/*
 * Copyright (c) 2020. This source code is licensed under MIT. A copy of this license is available with the source code in the LICENSE.md file.
 */

import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.InventoryHtmlReportRenderer
import com.github.jk1.license.render.SimpleHtmlReportRenderer
import com.github.jk1.license.render.TextReportRenderer

plugins {
    id 'java'
    id 'kr.entree.spigradle' version '1.2.4'
    id "net.ltgt.apt-idea" version "0.21"
    id 'idea'
    id 'checkstyle'
    id "com.github.jk1.dependency-license-report" version "1.12"
    id "com.github.ben-manes.versions" version "0.27.0"
    id "net.rdrei.android.buildtimetracker" version "0.11.0"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: "checkstyle"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'me.abaw7'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile paper('1.15.2')
}

spigot {
    authors = ['Abaw7']
    apiVersion = '1.15'
    load = STARTUP
}

checkstyle {
    toolVersion = "8.10.1"
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    configProperties = [
            'checkstyle.cache.file': "${buildDir}/checkstyle.cache",
    ]
    ignoreFailures = true
    showViolations = true
}


licenseReport {
    filters = new LicenseBundleNormalizer()
    renderers = [new InventoryHtmlReportRenderer('report.html', 'Backend'), new SimpleHtmlReportRenderer(), new TextReportRenderer()]
}

task cacheDeps(type: Exec) {
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
}

task copyDeps(type: Copy) {
    from configurations.compile
    into "${project.buildDir}/deps"
}

build.dependsOn copyDeps